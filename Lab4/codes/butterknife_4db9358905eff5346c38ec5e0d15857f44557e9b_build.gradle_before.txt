apply plugin: 'java-library'
apply plugin: 'checkstyle'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

def logger = new com.android.build.gradle.internal.LoggerWrapper(project.logger)
def sdkHandler = new com.android.build.gradle.internal.SdkHandler(project, logger)
for (File file : sdkHandler.sdkLoader.repositories) {
  repositories.maven {
    url = file.toURI()
  }
}

dependencies {
  implementation project(':butterknife-annotations')
  implementation deps.auto.common
  api deps.javapoet
  compileOnly deps.auto.service
  compileOnly files(org.gradle.internal.jvm.Jvm.current().getToolsJar())

  testImplementation deps.junit
  testImplementation deps.truth
}

checkstyle {
  configFile rootProject.file('checkstyle.xml')
  showViolations true
  //Remove this when tests are less verbose, i.e. using JavaPoet
  sourceSets = [sourceSets.main]
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
