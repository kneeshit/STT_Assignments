apply plugin: 'com.android.application'

android {
  compileSdkVersion versions.compileSdk

  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
  }

  defaultConfig {
    applicationId 'com.example.butterknife'
    minSdkVersion versions.minSdk
    targetSdkVersion versions.compileSdk
    versionCode 1
    versionName '1.0.0'
  }

  lintOptions {
    textReport true
    textOutput "stdout"
    checkAllWarnings true
    warningsAsErrors true
    disable 'UnknownNullness'
    showAll true
    explainIssues true
    // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks.
    checkReleaseBuilds false
  }

  buildTypes {
    debug {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
    }
  }

  productFlavors {
    flavorDimensions 'runtime'

    reflect {
      dimension 'runtime'
      applicationIdSuffix '.reflect'
    }
    codegen {
      dimension 'runtime'
      applicationIdSuffix '.codegen'
    }
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
}

dependencies {
  reflectImplementation project(':butterknife-reflect')

  codegenImplementation project(':butterknife')
  codegenAnnotationProcessor project(':butterknife-compiler')

  testImplementation deps.junit
  testImplementation deps.truth
  testImplementation deps.robolectric
}
